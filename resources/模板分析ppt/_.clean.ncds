:<:{模板概览}
    <= 完成模板分析，自动生成演示大纲，输出最终PPT
    
    <- {输入目录}
    <- {输出目录}

    <- {演示主题}
    <- {目标受众}
    <- {期望长度}

    <- {模板相对路径}
    
    <- {模板路径}
        <= 组合完整模板路径
        <- {输入目录}
        <- {模板相对路径}
    
    <- [内容参考元数据]
    <- [风格参考元数据]
    
    <- [内容参考]
        <= 对于内容参考元数据中的每个元数据
            <= 返回此迭代的加载内容
            <- {加载的内容}
                <= 从文件路径加载内容
                <- {当前内容元数据}
                <- {输入目录}
        <- [内容参考元数据]
        <* {当前内容元数据}
    
    <- {所有内容参考}
        <= 将所有内容参考收集到单个对象中
        <- [内容参考]
    
    <- [风格参考]
        <= 对于风格参考元数据中的每个元数据
            <= 返回此迭代的加载风格
            <- {加载的风格}
                <= 从文件路径加载风格
                <- {当前风格元数据}
                <- {输入目录}
        <- [风格参考元数据]
        <* {当前风格元数据}
    
    <- {所有风格参考}
        <= 将所有风格参考收集到单个对象中
        <- [风格参考]
    
    <- {需求分析}
        <= 分析演示文稿需求：主题深度、受众水平、语调、关键主题
        <- {演示主题}
        <- {目标受众}
        <- {期望长度}
        <- {所有内容参考}
        <- {所有风格参考}
    
    <- {已保存的需求分析}
        <= 将需求分析保存到输出目录
        <- {需求分析}
        <- {输出目录}
        <- {相对路径：中间产物/需求分析.json}
    
    <- {模板原始数据}
        <= 提取模板所有页面内容
        <- {模板路径}
    
    <- {已保存的模板原始数据}
        <= 将模板原始数据保存为JSON
        <- {模板原始数据}
        <- {输出目录}
        <- {相对路径：模板原始数据.json}
    
    <- [页面列表]
        <= 从模板原始数据中提取页面列表
        <- {模板原始数据}
    
    <- [所有单页总结]
        <= 对于页面列表中的每个页面
            <= 返回此迭代的页面分析结果
            
            <- {结构特征}
                <= 识别页面的基础结构特征
                <- {当前页面}
            
            <- {页面意图}
                <= 理解这一页想表达什么
                <- {当前页面}
                <- {结构特征}
            
            <- {页面分析}
                <= 基于意图判断每个元素的可替换性
                <- {当前页面}
                <- {结构特征}
                <- {页面意图}

            <- {单页总结}
                <= 从页面分析中提取简化总结
                <- {页面分析}
            
            <- {已保存的页面分析}
                <= 将页面分析保存到输出目录
                <- {页面分析}
                <- {输出目录}
                <- {页面分析相对路径}
                    <= 构造当前页面的保存路径
                    <- {当前页面}
            
        <- [页面列表]
        <* {当前页面}
    
    <- {简化概览}
        <= 将所有单页总结收集为简化概览
        <- [所有单页总结]
    
    <- {已保存的简化概览}
        <= 将简化概览保存到输出目录
        <- {简化概览}
        <- {输出目录}
        <- {相对路径：简化概览.json}
    
    <- {演示大纲}
        <= 基于分析结果自动生成演示大纲
        <- {演示主题}
        <- {目标受众}
        <- {期望长度}
        <- {需求分析}
        <- {所有内容参考}
        <- {简化概览}
    
    <- {已保存的演示大纲}
        <= 将演示大纲保存到输出目录
        <- {演示大纲}
        <- {输出目录}
        <- {相对路径：中间产物/演示大纲.json}
    
    <- {模板计划}
        <= 根据演示大纲和简化概览生成模板计划
        <- {演示大纲}
        <- {简化概览}
        <- {需求分析}
    
    <- {已保存的模板计划}
        <= 将模板计划保存到输出目录
        <- {模板计划}
        <- {输出目录}
        <- {相对路径：中间产物/模板计划.json}
    
    <- {对齐大纲}
        <= 将演示大纲与模板计划对齐，生成详细内容计划
        <- {演示大纲}
        <- {模板计划}
        <- {简化概览}
        <- {所有内容参考}
        <- {需求分析}
    
    <- {已保存的对齐大纲}
        <= 将对齐大纲保存到输出目录
        <- {对齐大纲}
        <- {输出目录}
        <- {相对路径：中间产物/对齐大纲.json}

    <- {基础演示文稿}
        <= 根据模板计划生成基础PPTX
        <- {模板路径}
        <- {模板计划}
        <- {输出目录}
        <- {相对路径：中间产物/基础演示文稿.pptx}

    <- [大纲页面列表]
        <= 从对齐大纲中提取 sections 列表
        <- {对齐大纲}
    
    <- [所有页面内容]
        <= 对于大纲页面列表中的每个页面
            <= 返回此迭代的页面内容
            
            <- {此页分析}
                <= 加载此页的详细页面分析
                <- {输出目录}
                <- {页面分析路径}
                    <= 构造页面分析文件路径
                    <- {当前大纲页面}

            <- {分析简化版本}
                <= 将页面分析转换为简化的 slot 格式
                <- {此页分析}

            <- {可更改元素映射}
                <= 从页面分析提取 slot 到 shape_index 的映射
                <- {此页分析}
            
            <- {页面内容草稿}
                <= 根据大纲和简化分析生成内容草稿
                <- {当前大纲页面}
                <- {分析简化版本}
            
            <- {页面文本内容}
                <= 使用内容参考和风格参考纠正和充实草稿
                <- {页面内容草稿}
                <- {所有内容参考}
                <- {所有风格参考}

            <- {映射页面文本内容}
                <= 将 slot 映射回 shape_index
                <- {页面文本内容}
                <- {可更改元素映射}
            
            <- {已保存的页面内容}
                <= 将页面内容保存到输出目录
                <- {映射页面文本内容}
                <- {输出目录}
                <- {页面内容相对路径}
                    <= 构造当前页面内容的保存路径
                    <- {当前大纲页面}
            
        <- [大纲页面列表]
        <* {当前大纲页面}
    
    <- {所有替换计划}
        <= 将所有页面内容收集为替换计划列表
        <- [所有页面内容]
    
    <- {最终演示文稿}
        <= 将所有内容填充到基础演示文稿
        <- {基础演示文稿}
        <- {所有替换计划}
        <- {输出目录}
        <- {相对路径：最终演示文稿.pptx}
    
    <- {优化后的演示文稿}
        <= 检测并修复文本溢出问题
        <- {最终演示文稿}
        <- {输出目录}
        <- {相对路径：最终演示文稿.pptx}
    
    <- {模板概览输出}
        <= 收集所有生成的输出
        <- {优化后的演示文稿}
        <- {简化概览}
        <- {对齐大纲}
        <- {需求分析}


